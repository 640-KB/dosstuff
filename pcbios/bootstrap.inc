%ifndef __BOOTSTRAP_INC__
%define __BOOTSTRAP_INC__

;--- INT 19 -----------------------------------------------------
; BOOT STRAP LOADER						:
;	IF A 5 1/4" DISKETTE DRIVE IS AVAILABLE ON THE SYSTEM,  :
;	TRACK 0, SECTOR 1 IS READ INTO THE BOOT LOCATION	:
;	(SEGMENT 0, OFFSET 7C00) AND CONTROL IS TRANSFERRED	:
;	THERE.							:
;								:
;	IF THERE IS NO DISKETTE DRIVE, OR IF THERE IS A 	:
;	HARDWARE ERROR CONTROL IS TRANSFERRED TO THE RESIDENT	:
;	BASIC ENTRY POINT.					:
;								:
; IPL ASSUMPTIONS						:
;	8255 PORT 60H BIT 0 = 1 IF IPL FROM DISKETTE		:
;----------------------------------------------------------------

;----- IPL WAS SUCCESSFUL

H4:
	JMP	0:BOOT_LOCN
	TIMES	0E6F2H-($-$$) DB 0FFh
BOOT_STRAP:
	STI				; ENABLE INTERRUPTS
	SUB	AX,AX
	MOV	DS,AX

;----- RESET DISKETTE PARAMETER TABLE VECTOR

	MOV	WORD [DISK_POINTER],DISK_BASE
	MOV	WORD [DISK_POINTER+2],CS
	MOV	AX,[DATA_WORD+BiosData.equipFlag] ; GET THE EQUIPMENT SWITCHES
	TEST	AL,1			; ISOLATE IPL SENSE SWITCH
	JZ	H3			; GO TO CASSETTE BASIC ENTRY POINT

;----- MUST LOAD SYSTEM FROM DISKETTE -- CX HAS RETRY COUNT

	MOV	CX,4			; SET RETRY COUNT
H1:					; IPL_SYSTEM
	PUSH	CX			; SAVE RETRY COUNT
	MOV	AH,0			; RESET THE DISKETTE SYSTEM
	INT	13H			; DISKETTE_IO
	JC	H2			; IF ERROR, TRY AGAIN
	MOV	AX,201H 		; READ IN THE SINGLE SECTOR
	SUB	DX,DX
	MOV	ES,DX
	MOV	BX,BOOT_LOCN
	MOV	CX,1			; SECTOR 1, TRACK 0
	INT	13H			; DISKETTE_IO
H2:	POP	CX			; RECOVER RETRY COUNT
	JNC	H4			; CF SET BY UNSUCCESSFUL READ
	LOOP	H1			; DO IT FOR RETRY TIMES

;------ UNABLE TO IPL FROM THE DISKETTE

H3:					; CASSETTE_JUMP:
	INT	18H			; USE INTERRUPT VECTOR TO GET TO BASIC

%endif ; !__BOOTSTRAP_INC__